plugins {
    id 'java'
    id 'maven-publish'
}

version = "1.1.0"
group = "com.moulberry.mixinconstraints"

java {
    sourceCompatibility = "8"
    targetCompatibility = "8"
}

repositories {
    mavenCentral()
    maven{ url = "https://maven.fabricmc.net/" }
    maven{ url = "https://repo.spongepowered.org/maven" }
    maven{ url = "https://maven.neoforged.net/releases" }
}

dependencies {
    compileOnly("org.spongepowered:mixin:0.8.5")
    compileOnly("org.ow2.asm:asm-tree:9.7")
    compileOnly("org.apache.logging.log4j:log4j-api:2.19.0")
    compileOnly("org.jetbrains:annotations:24.1.0")
}

tasks.register("sourcesJar", Jar) {
    group = "build"
    from sourceSets.main.allSource
    archiveClassifier = "sources"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "com.moulberry.mixinconstraints"
            artifactId = "mixinconstraints"

            from components.java

            artifact source: sourcesJar, classifier: 'sources', extension: 'jar'

            pom {
                name = "MixinConstraints"
                description = "Library to enable/disable mixins using annotations"
                url = "https://github.com/Moulberry/MixinConstraints"
                inceptionYear = "2024"
                packaging = "jar"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://opensource.org/license/mit"
                    }
                }

                developers {
                    developer {
                        name = "Moulberry"
                        url = "https://github.com/Moulberry"
                    }
                }

                contributors {
                    contributor {
                        name = "rdh"
                        url = "https://github.com/rhysdh540"
                    }
                    contributor {
                        name = "IThundxr"
                        url = "https://ithundxr.dev"
                    }
                }

                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/Moulberry/MixinConstraints/issues"
                }

                scm {
                    url = "https://github.com/Moulberry/MixinConstraints/"
                    connection = "scm:git:git://github.com/Moulberry/MixinConstraints.git"
                    developerConnection = "scm:git:ssh://git@github.com/Moulberry/MixinConstraints.git"
                }
            }
        }
    }
}